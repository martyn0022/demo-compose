<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:hl7="urn:hl7-org:v3" exclude-result-prefixes="hl7">
	<!-- AlsoInclude: Comment.xsl -->
	
	<xsl:template match="hl7:act" mode="eCn-Condition">
		<Problem>

			<!--
				Field : Problem Category
				Target: HS.SDA3.Problem Category
				Target: /Container/Problems/Problem/Category
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/code
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/code
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/code/@code
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/code/@code
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:if test="string-length(../../hl7:code/@code) or (hl7:entryRelationship/hl7:observation/hl7:code)">
				<xsl:apply-templates select="hl7:entryRelationship/hl7:observation/hl7:code" mode="fn-CodeTable">
					<xsl:with-param name="hsElementName" select="'Category'"/>
				</xsl:apply-templates>
			</xsl:if>
			
			<!-- Identifiers -->
			<!--
				Field : Problem Identifiers
				Target: HS.SDA3.Problem.Identifiers Identifier
				Target: /Container/Problems/Identifiers/Identifier
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/id
			-->
			<xsl:apply-templates select="hl7:entryRelationship/hl7:observation" mode="eCn-Condition-identifiers"/>
			
			<!--
				Field : Problem Id
				Target: HS.SDA3.Problem SdaId
				Target: /Container/Problems/Problem/SdaId
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/id
			-->
			<xsl:apply-templates select="hl7:id" mode="fn-SdaId"/>
			
			<!--
				Field : Problem Encounter
				Target: HS.SDA3.Problem EncounterNumber
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/EncounterNumber
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/encounter/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/encounter/id
				Note  : If the CDA encounter link @extension is present then
						it is imported to SDA EncounterNumber.  Otherwise if
						the encounter link @root is present then it is used.
						If there is no encounter link on the CDA Problem and
						there is an encompassingEncounter in the CDA document
						header then the id from the encompassingEncounter is
						imported to SDA EncounterNumber.
			-->
			<EncounterNumber><xsl:apply-templates select="." mode="fn-EncounterID-Entry"/></EncounterNumber>

			<xsl:apply-templates select="hl7:entryRelationship/hl7:observation" mode="eCn-Condition-observation"/>

			<!--
				Field : Problem Status
				Target: HS.SDA3.Problem Status
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Status
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/statusCode
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/statusCode
				StructuredMappingRef: CodeTableDetail
				Note  : This mapping is only used if an alternative observation statusCode is not present
			-->
			<xsl:if test="not(hl7:entryRelationship/hl7:observation/hl7:entryRelationship/hl7:observation[hl7:code/@code='33999-4']/hl7:value)">
				<xsl:apply-templates select="hl7:statusCode" mode="fn-CodeTable">
					<xsl:with-param name="hsElementName" select="'Status'"/>
				</xsl:apply-templates>
			</xsl:if>

			<!--
				Field : Problem Comments
				Target: HS.SDA3.Problem Comments
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Comments
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/act[code/@code='48767-8']/text
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/act[code/@code='48767-8']/text
			-->
			<xsl:apply-templates select="." mode="eCm-Comment"/>
			
			<!--
				Field : Problem Author (observation)
				Target: HS.SDA3.Problem EnteredBy
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/EnteredBy
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/author
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/author
				StructuredMappingRef: EnteredByDetail
				Note  : CDA author information for the Problem may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredBy use of author in the CDA document, if author is not found at
				        either level then document-level author person is used.
			-->
			<!--
				Field : Problem Author (act)
				Target: HS.SDA3.Problem EnteredBy
				Target: /Container/Problems/Problem/EnteredBy
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/author
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/author
				StructuredMappingRef: EnteredByDetail
				Note  : CDA author information for the Problem may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredBy use of author in the CDA document, if author is not found at
				        either level then document-level author person is used.
			-->
			<xsl:call-template name="EnteredBy">
				<xsl:with-param name="actNode" select="."/>
			</xsl:call-template>
			
			<!--
				Field : Problem Information Source (observation)
				Target: HS.SDA3.Problem EnteredAt
				Target: /Container/Problems/Problem/EnteredAt
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/informant
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/informant
				StructuredMappingRef: EnteredAt
				Note  : CDA informant information for the Problem may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own informant data). As with all other
				        EnteredAt use of informant in the CDA document, if informant is not found at
				        either level then document-level informant is used.
			-->
			<!--
				Field : Problem Information Source (act)
				Target: HS.SDA3.Problem EnteredAt
				Target: /Container/Problems/Problem/EnteredAt
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/informant
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/informant
				StructuredMappingRef: EnteredAt
				Note  : CDA informant information for the Problem may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own informant data). As with all other
				        EnteredAt use of informant in the CDA document, if informant is not found at
				        either level then document-level informant is used.
			-->
			<xsl:call-template name="EnteredAt">
				<xsl:with-param name="actNode" select="."/>
			</xsl:call-template>
			
			<!--
				Field : Problem Author Time (observation)
				Target: HS.SDA3.Problem EnteredOn
				Target: /Container/Problems/Problem/EnteredOn
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/author/time/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/author/time/@value
				Note  : CDA author information for the Problem may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredOn use of author in the CDA document, if author is not found at
				        either level then document-level author is used.
			-->
			<!--
				Field : Problem Author Time (act)
				Target: HS.SDA3.Problem EnteredOn
				Target: /Container/Problems/Problem/EnteredOn
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/author/time/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/author/time/@value
				Note  : CDA author information for the Problem may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredOn use of author in the CDA document, if author is not found at
				        either level then document-level author is used.
			-->
			<xsl:call-template name="EnteredOn">
				<xsl:with-param name="actNode" select="."/>
			</xsl:call-template>
			
			<!--
				Field : Problem Concern Act Id
				Target: HS.SDA3.Problem ExternalId
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/ExternalId
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/id
				StructuredMappingRef: ExternalId
			-->
			<xsl:apply-templates select="." mode="fn-ExternalId"/>
			
			<!-- Custom SDA Data-->
			<xsl:apply-templates select="." mode="eCn-ImportCustom-Problem"/>
		</Problem>
	</xsl:template>

	<xsl:template match="hl7:observation" mode="eCn-Condition-identifiers">

		<!--
			Field : Problem Identifiers
			Target: HS.SDA3.Problem.Identifiers Identifier
			Target: /Container/Problems/Identifiers/Identifier
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/id
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/id
		-->
		<xsl:apply-templates select="." mode="fn-Identifiers"/>

	</xsl:template>

	<xsl:template match="hl7:observation" mode="eCn-Condition-observation">

			<!--
				Field : Problem Verification Status
				Target: HS.SDA3.Problem VerificationStatus
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Problem/ProblemDetails
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/@negationInd
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/@negationInd
				Note: will be left undefined if negationInd is false or undefined
			-->
			<xsl:if test="@negationInd='true'">
				<VerificationStatus>
					<Code>refuted</Code>
					<System>http://hl7.org/fhir/ValueSet/condition-ver-status</System>
					<Description>Refuted</Description>
				</VerificationStatus>
			</xsl:if>

			<!-- Identification Time -->
			<!--
				Field : Diagnosis Identification Date/Time
				Target: HS.SDA3.Diagnosis IdentificationTime
				Target: /Container/Diagnoses/Diagnosis/IdentificationTime
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/entryRelationship/act[code/@code='77975-1']/effectiveTime/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/entryRelationship/act[code/@code='77975-1']/effectiveTime/@value
			-->
			<xsl:apply-templates select="hl7:entryRelationship/hl7:act[hl7:code/@code='77975-1']/hl7:effectiveTime" mode="eCn-IdentificationTime"/>

			<!-- Problem Details -->
			<!--
				Field : Problem Name
				Target: HS.SDA3.Problem ProblemDetails
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/ProblemDetails
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/entryRelationship/act[code/@code='48767-8']/text
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/entryRelationship/act[code/@code='48767-8']/text
			-->
			<xsl:apply-templates select="hl7:entryRelationship/hl7:act[hl7:code/@code='48767-8']/hl7:text" mode="eCn-ProblemDetails"/>

			<!-- OnsetAge -->
			<!--
				Field : Problem Onset Age
				Target: HS.SDA3.Problem OnsetAge
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/OnsetAge
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SUBJ' and @inversionInd='true']/observation[code/@code='445518008']/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SUBJ' and @inversionInd='true']/observation[code/@code='445518008']/value
			-->
			<xsl:apply-templates select="hl7:entryRelationship[@typeCode='SUBJ' and @inversionInd='true']/hl7:observation[hl7:code/@code='445518008']/hl7:value" mode="eCn-OnsetAge"/>

			<!-- SupportingObservation -->
			<!--
				Field : Problem Supporting Observation
				Target: HS.SDA3.Problem SupportingObservation
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/SupportingObservation
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SPRT']/observation/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SPRT']/observation/id
			-->
			<xsl:if test="hl7:entryRelationship[@typeCode='SPRT']/hl7:observation">
				<SupportingObservation>
					<xsl:apply-templates select="hl7:entryRelationship[@typeCode='SPRT']/hl7:observation/hl7:id" mode="eCn-SupObs"/>
				</SupportingObservation>
			</xsl:if>

			<!-- Problem Details -->
			<!--
				Field : Problem ProblemDetails
				Target: HS.SDA3.Problem ProblemDetails
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Problem/ProblemDetails
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/text
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/text
			-->
			<xsl:apply-templates select="hl7:text" mode="eCn-ProblemDetails"/>			

			<!--
				Field : Problem Code
				Target: HS.SDA3.Problem Problem
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Problem
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/value
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:apply-templates select="hl7:value" mode="fn-CodeTable">
				<xsl:with-param name="hsElementName" select="'Problem'"/>
			</xsl:apply-templates>
			
			<!--
				Field : Problem Status
				Target: HS.SDA3.Problem Status
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Status
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/entryRelationship/observation[code/@code='33999-4']/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/entryRelationship/observation[code/@code='33999-4']/value
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:apply-templates select="hl7:entryRelationship/hl7:observation[hl7:code/@code='33999-4']/hl7:value" mode="fn-CodeTable">
				<xsl:with-param name="hsElementName" select="'Status'"/>
			</xsl:apply-templates>

			<!--
				Field : Problem Treating Provider
				Target: HS.SDA3.Problem Clinician
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Clinician
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/performer
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/performer
				StructuredMappingRef: Clinician
				Note  : Import gets SDA Clincian from either entry/act/performer
						or entry/act/entryRelationship/observation/performer.
						A CDA Problem entry will have either one or the other.
			-->
			<!--
				Field : Problem Treating Provider
				Target: HS.SDA3.Problem Clinician
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Clinician
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/performer
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/performer
				StructuredMappingRef: Clinician
				Note  : Import gets SDA Clincian from either entry/act/performer
						or entry/act/entryRelationship/observation/performer.
						A CDA Problem entry will have either one or the other.
			-->
			<xsl:apply-templates select="hl7:performer | ../../hl7:performer" mode="fn-Clinician"/>

			<!--
				Field : Problem Start Date/Time
				Target: HS.SDA3.Problem FromTime
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/FromTime
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/effectiveTime/low/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/effectiveTime/low/@value
			-->
			<!--
				Field : Problem End Date/Time
				Target: HS.SDA3.Problem ToTime
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/ToTime
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/effectiveTime/high/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/effectiveTime/high/@value
			-->
			<xsl:apply-templates select="hl7:effectiveTime/hl7:low" mode="fn-FromTime"/>
			<xsl:apply-templates select="hl7:effectiveTime/hl7:high" mode="fn-ToTime"/>

	</xsl:template>

	<xsl:template match="hl7:act/hl7:entryRelationship" mode="eCn-Diagnosis">
		<xsl:param name="diagnosisType"/>
		<xsl:param name="encounterID" select="''"/>

		<Diagnosis>
			<!--
				Field : Diagnosis Id
				Target: HS.SDA3.Diagnosis SdaId
				Target: /Container/Diagnoses/Diagnosis/SdaId
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/id
			-->
			<xsl:choose>
				<xsl:when test="hl7:observation/hl7:id and not(hl7:observation/hl7:id/@nullFlavor)">
					<xsl:apply-templates select="hl7:observation/hl7:id" mode="fn-SdaId"/>
				</xsl:when>
				<xsl:when test="hl7:observation/hl7:id/@nullFlavor">
					<SdaID>
						<xsl:value-of select="generate-id(hl7:observation/hl7:id)"/>
					</SdaID>
				</xsl:when>
				<xsl:otherwise>
					<SdaID>
						<xsl:value-of select="generate-id(hl7:observation)"/>
					</SdaID>
				</xsl:otherwise>
			</xsl:choose>
			
			<!--
				Field : Diagnosis Encounter
				Target: HS.SDA3.Diagnosis EncounterNumber
				Target: /Container/Diagnoses/Diagnosis/EncounterNumber
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/encounter/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/encounter/id
				Note  : If the CDA encounter link @extension is present then
						it is imported to SDA EncounterNumber.  Otherwise if
						the encounter link @root is present then it is used.
						If there is no encounter link on the CDA Diagnosis and
						there is an encompassingEncounter in the CDA document
						header then the id from the encompassingEncounter is
						imported to SDA EncounterNumber.
			-->
			<EncounterNumber>
				<xsl:choose>
					<xsl:when test="not(string-length($encounterID))"><xsl:apply-templates select="." mode="fn-EncounterID-Entry"/></xsl:when>
					<xsl:otherwise><xsl:value-of select="$encounterID"/></xsl:otherwise>
				</xsl:choose>
			</EncounterNumber>

			<!--
				Field : Diagnosis Code
				Target: HS.SDA3.Diagnosis Diagnosis
				Target: /Container/Diagnoses/Diagnosis/Diagnosis
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/value
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:apply-templates select="hl7:observation/hl7:value" mode="fn-CodeTable">
				<xsl:with-param name="hsElementName" select="'Diagnosis'"/>
				<xsl:with-param name="importOriginalText" select="'1'"/>
			</xsl:apply-templates>

			<!--
				DiagnosisType is not imported from a specific CDA field.  It is
				determined by the CDA section that is currently being imported -
				either Hospital Admission Diagnosis (A|Admitting) or Discharge
				Diagnosis (D|Discharge).
			-->
			<xsl:if test="string-length($diagnosisType)">
				<DiagnosisType>
					<Code><xsl:value-of select="substring-before($diagnosisType, '|')"/></Code>
					<Description><xsl:value-of select="substring-after($diagnosisType, '|')"/></Description>
				</DiagnosisType>
			</xsl:if>
			
			<!--
				Field : Diagnosis Treating Provider
				Target: HS.SDA3.Diagnosis Clinician
				Target: /Container/Diagnoses/Diagnosis/Clinician
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/performer
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/performer
				StructuredMappingRef: Clinician
				Note  : Import gets SDA Clincian from either entry/act/performer
						or entry/act/entryRelationship/observation/performer.
						A CDA Diagnosis entry will have either one or the other.
			-->
			<!--
				Field : Diagnosis Treating Provider
				Target: HS.SDA3.Diagnosis Clinician
				Target: /Container/Diagnoses/Diagnosis/Clinician
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/performer
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/performer
				StructuredMappingRef: Clinician
				Note  : Import gets SDA Clincian from either entry/act/performer
						or entry/act/entryRelationship/observation/performer.
			-->
			<xsl:choose>
				<xsl:when test="../hl7:performer">
					<xsl:apply-templates select="../hl7:performer" mode="fn-DiagnosingClinician"/>
				</xsl:when>
				<xsl:when test="hl7:observation/hl7:performer">
					<xsl:apply-templates select="hl7:observation/hl7:performer" mode="fn-DiagnosingClinician"/>
				</xsl:when>
			</xsl:choose>

			<!-- Identification Time -->
			<xsl:apply-templates select="hl7:observation/hl7:effectiveTime" mode="eCn-IdentificationTime"/>
			
			<!--
				Field : Diagnosis Status
				Target: HS.SDA3.Diagnosis Status
				Target: /Container/Diagnoses/Diagnosis/Status
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/entryRelationship/observation[code/@code='33999-4']/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/entryRelationship/observation[code/@code='33999-4']/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/statusCode
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/statusCode
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:choose>
				<xsl:when test="hl7:observation/hl7:entryRelationship[@typeCode='REFR']/hl7:observation[hl7:code/@code='33999-4']/hl7:value">
					<xsl:apply-templates select="hl7:observation/hl7:entryRelationship[@typeCode='REFR']/hl7:observation[hl7:code/@code='33999-4']/hl7:value" mode="fn-CodeTable">
						<xsl:with-param name="hsElementName" select="'Status'"/>
					</xsl:apply-templates>
				</xsl:when>
				<xsl:when test="hl7:observation/hl7:statusCode">
					<xsl:apply-templates select="hl7:observation/hl7:statusCode" mode="fn-CodeTable">
						<xsl:with-param name="hsElementName" select="'Status'"/>
					</xsl:apply-templates>
				</xsl:when>
				<xsl:otherwise>
					<xsl:apply-templates select="../hl7:statusCode" mode="fn-CodeTable">
						<xsl:with-param name="hsElementName" select="'Status'"/>
					</xsl:apply-templates>
				</xsl:otherwise>
			</xsl:choose>

			<!--
				Field : Diagnosis Author (observation)
				Target: HS.SDA3.Diagnosis EnteredBy
				Target: /Container/Diagnoses/Diagnosis/EnteredBy
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/author
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/author
				StructuredMappingRef: EnteredByDetail
				Note  : CDA author information for the Diagnosis may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredBy use of author in the CDA document, if author is not found at
				        either level then document-level author person is used.
			-->
			<!--
				Field : Diagnosis Author (act)
				Target: HS.SDA3.Diagnosis EnteredBy
				Target: /Container/Diagnoses/Diagnosis/EnteredBy
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/author
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/author
				StructuredMappingRef: EnteredByDetail
				Note  : CDA author information for the Diagnosis may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredBy use of author in the CDA document, if author is not found at
				        either level then document-level author person is used.
			-->
			<xsl:call-template name="EnteredBy">
				<xsl:with-param name="actNode" select=".."/>
			</xsl:call-template>

			<!--
				Field : Diagnosis Information Source (observation)
				Target: HS.SDA3.Diagnosis EnteredAt
				Target: /Container/Diagnoses/Diagnosis/EnteredAt
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/informant
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/informant
				StructuredMappingRef: EnteredAt
				Note  : CDA informant information for the Diagnosis may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own informant data). As with all other
				        EnteredAt use of informant in the CDA document, if informant is not found at
				        either level then document-level informant is used.
			-->
			<!--
				Field : Diagnosis Information Source (act)
				Target: HS.SDA3.Diagnosis EnteredAt
				Target: /Container/Diagnoses/Diagnosis/EnteredAt
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/informant
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/informant
				StructuredMappingRef: EnteredAt
				Note  : CDA informant information for the Diagnosis may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own informant data). As with all other
				        EnteredAt use of informant in the CDA document, if informant is not found at
				        either level then document-level informant is used.
			-->
			<xsl:call-template name="EnteredAt">
				<xsl:with-param name="actNode" select=".."/>
			</xsl:call-template>

			<!--
				Field : Diagnosis Author Time (observation)
				Target: HS.SDA3.Diagnosis EnteredOn
				Target: /Container/Diagnoses/Diagnosis/EnteredOn
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/author/time/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/author/time/@value
				Note  : CDA author information for the Diagnosis may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredOn use of author in the CDA document, if author is not found at
				        either level then document-level author is used.
			-->
			<!--
				Field : Diagnosis Author Time (act)
				Target: HS.SDA3.Diagnosis EnteredOn
				Target: /Container/Diagnoses/Diagnosis/EnteredOn
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/author/time/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/author/time/@value
				Note  : CDA author information for the Diagnosis may be located at the observation
				        level (for specific observations) or at the act level (to apply it to all
				        observations that do not have their own author data). As with all other
				        EnteredOn use of author in the CDA document, if author is not found at
				        either level then document-level author is used.
			-->
			<xsl:call-template name="EnteredOn">
				<xsl:with-param name="actNode" select=".."/>
			</xsl:call-template>

			<!--
				Field : Diagnosis Id
				Target: HS.SDA3.Diagnosis ExternalId
				Target: /Container/Diagnoses/Diagnosis/ExternalId
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/id
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/id
				StructuredMappingRef: ExternalId
			-->
			<!-- Override ExternalId with the <id> values from the source CDA -->
			<xsl:apply-templates select="." mode="fn-ExternalId"/>
			
			<!-- Custom SDA Data-->
			<xsl:apply-templates select="." mode="eCn-ImportCustom-Diagnosis"/>
		</Diagnosis>
	</xsl:template>

	<xsl:template match="hl7:observation" mode="eCn-ProblemObservation">
			<!-- Problem Details -->
			<xsl:apply-templates select="hl7:text" mode="eCn-ProblemDetails"/>
			
			<!--
				Field : Problem Code
				Target: HS.SDA3.Problem Problem
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Problem
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/value
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:apply-templates select="hl7:value" mode="fn-CodeTable">
				<xsl:with-param name="hsElementName" select="'Problem'"/>
			</xsl:apply-templates>
			
			<!--
				Field : Problem Type
				Target: HS.SDA3.Problem Category
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Category
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/code
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/code
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:apply-templates select="hl7:code" mode="fn-CodeTable">
				<xsl:with-param name="hsElementName" select="'Category'"/>
			</xsl:apply-templates>
			
			<!--
				Field : Problem Treating Provider
				Target: HS.SDA3.Problem Clinician
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Clinician
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/performer
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/performer
				StructuredMappingRef: Clinician
				Note  : Import gets SDA Clincian from either entry/act/performer
						or entry/act/entryRelationship/observation/performer.
						A CDA Problem entry will have either one or the other.
			-->
			<!--
				Field : Problem Treating Provider
				Target: HS.SDA3.Problem Clinician
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Clinician
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/performer
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/performer
				StructuredMappingRef: Clinician
				Note  : Import gets SDA Clincian from either entry/act/performer
						or entry/act/entryRelationship/observation/performer.
						A CDA Problem entry will have either one or the other.
			-->
			<xsl:apply-templates select="hl7:performer" mode="fn-Clinician"/>
			
			<!--
				Field : Problem Status
				Target: HS.SDA3.Problem Status
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/Status
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/entryRelationship/observation[code/@code='33999-4']/value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/entryRelationship/observation[code/@code='33999-4']/value
				StructuredMappingRef: CodeTableDetail
			-->
			<xsl:apply-templates select="hl7:entryRelationship[@typeCode='REFR']/hl7:observation[hl7:code/@code='33999-4']/hl7:value" mode="fn-CodeTable">
				<xsl:with-param name="hsElementName" select="'Status'"/>
			</xsl:apply-templates>

			<!--
				Field : Problem Start Date/Time
				Target: HS.SDA3.Problem FromTime
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/FromTime
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/effectiveTime/low/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/effectiveTime/low/@value
			-->
			<!--
				Field : Problem End Date/Time
				Target: HS.SDA3.Problem ToTime
				Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/ToTime
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/effectiveTime/high/@value
				Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/effectiveTime/high/@value
			-->
			<xsl:apply-templates select="hl7:effectiveTime/hl7:low" mode="fn-FromTime"/>
			<xsl:apply-templates select="hl7:effectiveTime/hl7:high" mode="fn-ToTime"/>
	</xsl:template>

	<xsl:template match="hl7:effectiveTime" mode="eCn-IdentificationTime">
		<!--
			Field : Diagnosis Identification Date/Time
			Target: HS.SDA3.Diagnosis IdentificationTime
			Target: /Container/Diagnoses/Diagnosis/IdentificationTime
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.43']/entry/act/entryRelationship/observation/effectiveTime/@value
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.24']/entry/act/entryRelationship/observation/effectiveTime/@value
			Note  : If CDA effectiveTime/@value is not present
					then SDA IdentificationTime is imported from
					effectiveTime/low/@value instead.
		-->
		<xsl:choose>
			<xsl:when test="@value">
				<xsl:apply-templates select="@value" mode="fn-E-paramName-timestamp">
					<xsl:with-param name="emitElementName" select="'IdentificationTime'"/>
				</xsl:apply-templates>
			</xsl:when>
			<xsl:when test="hl7:low/@value">
				<xsl:apply-templates select="hl7:low/@value" mode="fn-E-paramName-timestamp">
					<xsl:with-param name="emitElementName" select="'IdentificationTime'"/>
				</xsl:apply-templates>
			</xsl:when>
		</xsl:choose>
	</xsl:template>

	<xsl:template match="hl7:text" mode="eCn-ProblemDetails">
		<!--
			Field : Problem Name
			Target: HS.SDA3.Problem ProblemDetails
			Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/ProblemDetails
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/text
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/text
		-->
		<ProblemDetails><xsl:apply-templates select="." mode="fn-TextValue"/></ProblemDetails>
	</xsl:template>

	<xsl:template match="hl7:value" mode="eCn-OnsetAge">
		<!--
			Field : Problem Onset Age
			Target: HS.SDA3.Problem OnsetAge
			Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/OnsetAge
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SUBJ' and @inversionInd='true']/observation[code/@code='445518008']/value
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SUBJ' and @inversionInd='true']/observation[code/@code='445518008']/value
		-->
		<xsl:if test="string-length(@value)">
			<OnsetAge>
				<Value><xsl:value-of select="@value"/></Value>
				<xsl:if test="string-length(@unit)">
					<Code><xsl:value-of select="@unit"/></Code>
				</xsl:if>
			</OnsetAge>
		</xsl:if>
	</xsl:template>

	<xsl:template match="hl7:id" mode="eCn-SupObs">
		<!--
			Field : Problem Supporting Observation
			Target: HS.SDA3.Problem SupportingObservation
			Target: /Container/Problems/Problem[not(Category/Code='248536006' or Category/Code='373930000')]/SupportingObservation
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.5.1']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SPRT']/observation/id
			Source: /ClinicalDocument/component/structuredBody/component/section[templateId/@root='2.16.840.1.113883.10.20.22.2.20']/entry/act/entryRelationship/observation/entryRelationship[@typeCode='SPRT']/observation/id
		-->
		<Reference>
			<xsl:apply-templates select="." mode="fn-SdaId"/>
			<Type>Observation</Type>
		</Reference>
	</xsl:template>
		
	<!--
		This empty template may be overridden with custom logic.
		The input node spec is normally $sectionRootPath/hl7:entry/hl7:act.
	-->
	<xsl:template match="*" mode="eCn-ImportCustom-Problem">
	</xsl:template>
	
	<!--
		This empty template may be overridden with custom logic.
		The input node spec is normally $sectionRootPath/hl7:entry/hl7:act.
	-->
	<xsl:template match="*" mode="eCn-ImportCustom-Diagnosis">
	</xsl:template>

	<xsl:template name="EnteredBy">
		<!-- Used in: Condition and Diagnosis transformations -->
		<xsl:param name="actNode"/>
		<xsl:choose>
			<xsl:when test="$actNode/hl7:entryRelationship/hl7:observation/hl7:author">
				<xsl:apply-templates select="$actNode/hl7:entryRelationship/hl7:observation" mode="fn-EnteredBy"/>
			</xsl:when>
			<xsl:when test="$actNode/hl7:author">
				<xsl:apply-templates select="$actNode" mode="fn-EnteredBy"/>
			</xsl:when>
			<xsl:otherwise>
				<xsl:apply-templates select="$actNode/hl7:entryRelationship/hl7:observation" mode="fn-EnteredBy"/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<xsl:template name="EnteredAt">
		<!-- Used in: Condition and Diagnosis transformations -->
		<xsl:param name="actNode"/>
		<xsl:choose>
			<xsl:when test="$actNode/hl7:entryRelationship/hl7:observation/hl7:informant">
				<xsl:apply-templates select="$actNode/hl7:entryRelationship/hl7:observation" mode="fn-EnteredAt"/>
			</xsl:when>
			<xsl:when test="$actNode/hl7:informant">
				<xsl:apply-templates select="$actNode" mode="fn-EnteredAt"/>
			</xsl:when>
			<xsl:otherwise>
				<xsl:apply-templates select="$actNode/hl7:entryRelationship/hl7:observation" mode="fn-EnteredAt"/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<xsl:template name="EnteredOn">
		<!-- Used in: Condition and Diagnosis transformations -->
		<xsl:param name="actNode"/>
		<xsl:choose>
			<xsl:when test="$actNode/hl7:entryRelationship/hl7:observation/hl7:author">
				<xsl:apply-templates select="$actNode/hl7:entryRelationship/hl7:observation/hl7:author/hl7:time" mode="fn-EnteredOn"/>
			</xsl:when>
			<xsl:when test="$actNode/hl7:author">
				<xsl:apply-templates select="$actNode/hl7:author/hl7:time" mode="fn-EnteredOn"/>
			</xsl:when>
			<xsl:otherwise>
				<xsl:apply-templates select="$actNode/hl7:entryRelationship/hl7:observation/hl7:author/hl7:time" mode="fn-EnteredOn"/>
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>
	
</xsl:stylesheet>